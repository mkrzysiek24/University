#lang plait

(module+ test
  (print-only-errors #t))

;; abstract syntax -------------------------------

(define-type Op
  (add) (sub) (mul) (div)
  (eql) (leq))

(define-type Exp
  (numE [n : Number])
  (opE [op : Op]
       [l : Exp]
       [r : Exp])
  ; null? exp
  (opEnull
       [exp : Exp])
  (ifE [b : Exp]
       [l : Exp]
       [r : Exp])
  (condE [cs : (Listof (Exp * Exp))])
  ; dodana lista jako typ wyrażenia
  (listE  [elems : (Listof Exp)])
  (carE   [listexp : Exp])
  (cdrE   [listexp : Exp])
  (consE  [elem : Exp] [listexp : Exp])
  (nullE))

;; parse ----------------------------------------

(define (parse [s : S-Exp]) : Exp
  (cond
    [(s-exp-match? `NUMBER s)
     (numE (s-exp->number s))]
    [(s-exp-match? `{if ANY ANY ANY} s)
     (ifE (parse (second (s-exp->list s)))
          (parse (third (s-exp->list s)))
          (parse (fourth (s-exp->list s))))]
    [(s-exp-match? `{cond ANY ...} s)
     (condE (parse-cond (rest (s-exp->list s))))]
    ; parsowanie null?
    [(s-exp-match? `{null? ANY} s)
      (opEnull (parse (first (rest (s-exp->list s)))))]       
    ; parsowanie listy
    [(s-exp-match? `{list ANY ...} s)
     (listE (parse-list (rest (s-exp->list s))))]
    ; parsowanie wyrażenia (cons elem list)
    [(or  (s-exp-match? `{cons ANY {list ANY ...}} s) 
          (s-exp-match? `{cons ANY {cons ANY ANY}} s)
          (s-exp-match? `{cons ANY {cdr ANY}} s))
     (consE (parse (second (s-exp->list s))) (parse (third (s-exp->list s))))]
    ; parsowanie wyrażenia (car list)
    [(or  (s-exp-match? `{car {list ANY ...}} s) 
          (s-exp-match? `{car {cons ANY ANY}} s)
          (s-exp-match? `{car {cdr ANY}} s))
     (carE (parse (second (s-exp->list s))))]
    ; parsowanie wyrażenia (cdr list)
    [(or  (s-exp-match? `{cdr {list ANY ...}} s) 
          (s-exp-match? `{cdr {cons ANY ANY}} s)
          (s-exp-match? `{cdr {cdr ANY}} s))
     (cdrE (parse (second (s-exp->list s))))]
    ; parsowanie nulla
    [(s-exp-match? `null s)
     (nullE)]
    [(s-exp-match? `{SYMBOL ANY ANY} s)
     (opE (parse-op (s-exp->symbol (first (s-exp->list s))))
          (parse (second (s-exp->list s)))
          (parse (third (s-exp->list s))))]
    [else (error 'parse "invalid input")]))

(define (parse-cond [ss : (Listof S-Exp)]) : (Listof (Exp * Exp))
  (type-case (Listof S-Exp) ss
    [empty
     empty]
    [(cons s ss)
     (if (s-exp-match? `{ANY ANY} s)
         (cons (pair (parse (first (s-exp->list s)))
                     (parse (second (s-exp->list s))))
               (parse-cond ss))
         (error 'parse "invalid input: cond"))]))

; parser list
(define (parse-list [ss : (Listof S-Exp)]) : (Listof Exp)
  (type-case (Listof S-Exp) ss
    [empty
     empty]
    [(cons s ss)
      (cons (parse s) (parse-list ss))]))

(define (parse-op [op : Symbol]) : Op
  (cond
    [(eq? op '+) (add)]
    [(eq? op '-) (sub)]
    [(eq? op '*) (mul)]
    [(eq? op '/) (div)]
    [(eq? op '=) (eql)]
    [(eq? op '<=) (leq)]
    [else (error 'parse (string-append "unknown operator " (symbol->string op)))]))

(module+ test
  (test (parse `2)
        (numE 2))
  (test (parse `{+ 2 1})
        (opE (add) (numE 2) (numE 1)))
  (test (parse `{* 3 4})
        (opE (mul) (numE 3) (numE 4)))
  (test (parse `{+ {* 3 4} 8})
        (opE (add)
             (opE (mul) (numE 3) (numE 4))
             (numE 8)))
  (test (parse `{if {= 0 1} {* 3 4} 8})
        (ifE (opE (eql) (numE 0) (numE 1))
             (opE (mul) (numE 3) (numE 4))
             (numE 8)))
   (test/exn (parse `{{+ 1 2}})
            "invalid input")
  (test/exn (parse `{+ 1})
            "invalid input")
  (test/exn (parse `{^ 1 2})
            "unknown operator")
  (test (parse `{cond {{= 0 1} {* 3 4}}
                      {{= 1 1} 8}})
        (condE (list (pair (opE (eql) (numE 0) (numE 1))
                           (opE (mul) (numE 3) (numE 4)))
                     (pair (opE (eql) (numE 1) (numE 1))
                           (numE 8)))))
  (test (parse `(list 1 2 3)) (listE (list (numE 1) (numE 2) (numE 3))))
  (test (parse `(car (list 1 2 3))) (carE (listE (list (numE 1) (numE 2) (numE 3))))))


; #|
;; eval --------------------------------------

(define-type Value
  (numV [n : Number])
  (boolV [b : Boolean])
  ; dodana lista jako typ wartości
  (listV [vals : (Listof Value)])
  ; null jako typ wartości
  (nullV))

(define (op-num-num->proc [f : (Number Number -> Number)]) : (Value Value -> Value)
  (λ (v1 v2)
    (type-case Value v1
      [(numV n1)
       (type-case Value v2
         [(numV n2)
          (numV (f n1 n2))]
         [else
          (error 'eval "type error")])]
      [else
       (error 'eval "type error")])))

(define (op-num-bool->proc [f : (Number Number -> Boolean)]) : (Value Value -> Value)
  (λ (v1 v2)
    (type-case Value v1
      [(numV n1)
       (type-case Value v2
         [(numV n2)
          (boolV (f n1 n2))]
         [else
          (error 'eval "type error")])]
      [else
       (error 'eval "type error")])))

; ewaluacja null? 
(define op-null->proc : (Value -> Value)
  (λ (v) (boolV (equal? v (nullV)))))

(define (op->proc [op : Op]) : (Value Value -> Value)
  (type-case Op op
    [(add) (op-num-num->proc +)]
    [(sub) (op-num-num->proc -)]
    [(mul) (op-num-num->proc *)]
    [(div) (op-num-num->proc /)]
    [(eql) (op-num-bool->proc =)]
    [(leq) (op-num-bool->proc <=)]))

(define (list-eval-helper [elems : (Listof Exp)]) : (Listof Value)
  (if (empty? elems)
      (list)
      (cons (eval (first elems)) (list-eval-helper (rest elems)))))

(define (cond->if [cs : (Listof (Exp * Exp))]) : Exp
  (type-case (Listof (Exp * Exp)) cs
    [empty
     (numE 42)]
    [(cons c cs)
     (ifE (fst c)
          (snd c )
          (cond->if cs))]))

(define (get-car [listexp : Exp]) : Value
      (let ([v (eval listexp)]) 
            (type-case Value v
                  [(listV vals) (first vals)]
                  [else (error 'get-car "not a list")])))

(define (get-cdr [listexp : Exp]) : Value
      (let ([v (eval listexp)]) 
            (type-case Value v
                  [(listV vals) (listV (rest vals))]
                  [else (error 'get-cdr "not a list")])))

(define (make-cons [e : Exp] [listexp : Exp]) : Value
      (let ([v (eval listexp)])
            (type-case Value v
                  [(listV vals) (listV (cons (eval e) vals))]
                  [else (error 'make-cons "not a list")])))

(define (eval [e : Exp]) : Value
  (type-case Exp e
    [(numE n) (numV n)]
    [(opE o l r) ((op->proc o) (eval l) (eval r))]
    [(opEnull exp) (op-null->proc (eval exp))]
    [(ifE b l r)
     (type-case Value (eval b)
       [(boolV v)
        (if v (eval l) (eval r))]
       [else
        (error 'eval "type error")])]
    [(condE cs)
     (eval (cond->if cs))]
    [(listE elems)
     (listV (list-eval-helper elems))]
    [(consE elem listexp) (make-cons elem listexp)]
    [(carE listexp) (get-car listexp)]
    [(cdrE listexp) (get-cdr listexp)]
    [(nullE) (nullV)]))

(define (run [e : S-Exp]) : Value
  (eval (parse e)))

(module+ test
  (test (run `2)
        (numV 2))
  (test (run `{+ 2 1})
        (numV 3))
  (test (run `{* 2 1})
        (numV 2))
  (test (run `{+ {* 2 3} {+ 5 8}})
        (numV 19))
  (test (run `{= 0 1})
        (boolV #f))
  (test (run `{if {= 0 1} {* 3 4} 8})
        (numV 8))
  (test (run `{cond {{= 0 1} {* 3 4}}
                    {{= 1 1} 8}})
        (numV 8)))

;; printer ———————————————————————————————————-

(define (value->string [v : Value]) : String
  (type-case Value v
    [(numV n) (to-string n)]
    [(boolV b) (if b "true" "false")]
    [(nullV) "null"]
    [(listV vals) (string-append "(" (string-append (foldr string-append "" (print-list-helper vals)) ")"))]))

(define (print-list-helper [vals : (Listof Value)]) : (Listof String)
  (cond [(empty? vals) '()]
        [(empty? (rest vals)) (cons (value->string (first vals)) (print-list-helper (rest vals)))]
        [else (cons (string-append (value->string (first vals)) " ") (print-list-helper (rest vals)))]))

(define (print-value [v : Value]) : Void
  (display (string-append (value->string v) "\n")))

(define (main [e : S-Exp]) : Void
  (print-value (eval (parse e))))

; ================ testy ====================

(module+ test
  (test (run `2)
        (numV 2))
  (test (run `{+ 2 1})
        (numV 3))
  (test (run `{* 2 1})
        (numV 2))
  (test (run `{+ {* 2 3} {+ 5 8}})
        (numV 19))
  (test (run `{= 0 1})
        (boolV #f))
  (test (run `{if {= 0 1} {* 3 4} 8})
        (numV 8))
  (test (run `{cond {{= 0 1} {* 3 4}}
                    {{= 1 1} 8}})
        (numV 8))
  (test (run `(car (list 1 2 3 4)))
        (numV 1))
  (test (run `(cons 1 (list 2 3)))
        (listV (list (numV 1) (numV 2) (numV 3))))
  (test (run `(cons 1 (cons 2 (cons 3 (list)))))
        (listV (list (numV 1) (numV 2) (numV 3))))
  (test (run `(cdr (list 1 2 3 4)))
        (listV (list (numV 2) (numV 3) (numV 4))))
  (test (run `(cdr (cdr (list 1 2 3 4))))
        (listV (list (numV 3) (numV 4))))
  (test (run `(car (cdr (list 1 2 3 4)))) ;
        (numV 2))
  (test (run `(cons 1 (cdr(list 2 3)))) ;
        (listV (list (numV 1) (numV 3))))
  (test (run `(cdr (cdr (cdr (list 2 3 4 5)))))
        (listV (list (numV 5))))
  (test (run `(cdr (cons 1 (list 2 3 4))))
        (listV (list (numV 2) (numV 3) (numV 4))))
  (test (run `(list 1 2))
        (listV (list (numV 1) (numV 2))))
  (test (run `(car (cons 1 (list 2 3))))
        (numV 1))
  (test (run `(null? 1))
        (boolV #f))
  (test (run `(null? (list 1 2 3)))
        (boolV #f))
  (test (run `(null? null))
        (boolV #t)))


(main `(cons 1 (cons 2 (list 3 4))))
(main `(car (cons 1 (cons 2 (list 3 4)))))
